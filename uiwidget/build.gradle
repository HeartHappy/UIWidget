plugins {
//    alias(libs.plugins.android.library)
//    alias(libs.plugins.kotlin.android)
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
    id 'maven-publish'
    id 'signing'
}
group("com.hearthappy")
version(libraryVersion)

tasks.register('javadoc', Javadoc) {
    source = android.sourceSets.main.java.sourceFiles
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

tasks.register('javadocJar', Jar) {
    dependsOn javadoc
    archiveClassifier.set("javadoc")
    from javadoc.destinationDir
}

tasks.register('sourceJar', Jar) {
    from android.sourceSets.main.java.srcDirs
    archiveClassifier.set("sources")
}
tasks.register('zipRepo', Zip) {
    from "$buildDir/repo"
    archiveFileName = 'io.zip' //
    destinationDirectory = file(buildDir)
}
afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.release
                groupId = 'io.github.hearthappy'
                artifactId = 'uiwidget'
                version = libraryVersion
                artifacts {
                    archives(tasks.named('javadocJar'))
                    archives(tasks.named('sourceJar'))
                }
                pom {
                    name = 'uiwidget'
                    description = 'Android basic framework packaging and extension library'
                    url = 'https://github.com/HeartHappy/UIWidget'

                    licenses {
                        license {
                            name = 'The Apache License, Version 2.0'
                            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        }
                    }

                    developers {
                        developer {
                            id = 'HeartHappy'
                            name = 'এ᭄白ຼ龙້໌ᮨꦿ'
                            email = '1096885636@qq.com'
                        }
                    }

                    scm {
                        connection = 'scm:git://github.com/HeartHappy/UIWidget'
                        developerConnection = 'scm:git:ssh://github.com/HeartHappy/UIWidget'
                        url = 'https://github.com/HeartHappy/UIWidget'
                    }
                }
            }
        }
        repositories {
            maven {
                //打包推送到本地仓库地址
                url = uri("$buildDir/repo")
                //推送远程仓库需要认证
//                url = uri("https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/")
//                credentials {
//                    username = project.properties['sonatypeUsername']
//                    password = project.properties['sonatypePassword']
//                }
            }
        }
    }
    signing {
        //gpg签名认证
        sign(publishing.publications.release)
    }
    // 设置 zipRepo 任务依赖于发布任务
    tasks.named('zipRepo') { dependsOn(publishReleasePublicationToMavenRepository) }

    // 确保发布任务完成后总是执行 zipRepo 任务，即使发布任务失败
    tasks.named('publishReleasePublicationToMavenRepository') {
        finalizedBy(tasks.named('zipRepo'))
    }
}
android {
    namespace 'com.hearthappy.uiwidget'
    compileSdk 31

    defaultConfig {
        minSdk 21

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        debug{
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    buildFeatures{
        viewBinding true
    }
}
// 禁用源码打包（避免生成 sources.jar）
tasks.withType(JavaCompile) {
    options.compilerArgs += ['-g:none'] // 移除调试信息
}
dependencies {

    implementation libs.androidx.core.ktx
    implementation libs.androidx.appcompat
    implementation libs.material
    implementation libs.com.github.bumptech.glide.glide
    implementation libs.kotlinx.coroutines.android
//    implementation libs.kotlin.reflect

}